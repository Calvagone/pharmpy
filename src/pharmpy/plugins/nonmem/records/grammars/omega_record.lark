// $OMEGA  [DIAGONAL(n)|BLOCK(n)|BLOCK(n) SAME(m)|BLOCK SAME(m)]
//         [BLOCK(n)VALUES(diag,odiag)]
//         [VARIANCE|STANDARD]  [COVARIANCE|CORRELATON] [CHOLESKY]
//         [[value1]  [value2]  [value3] ... [FIXED]]
//         [(value)xn]
//
// 4 legal forms
//   1. $OMEGA   [DIAGONAL(n)] [ v11 v22 v33 ... vnn ]
//   2. $OMEGA BLOCK(n) [ v11 v21 v22 v31 v32 v33 ... vn1 vn2 ... vnn ]
//   3. $OMEGA BLOCK(n) SAME(m)
//   4. $OMEGA BLOCK(n) VALUES(diag,odiag)

// Samples from nmhelp:
//   %input | BLOCK(3)  6. .005 .3 .0002 .006 .4|
//   %input | .04 .12|
//   %input | BLOCK(2) .04 .002 .12|
//   %input | BLOCK(2) SAME(3)|
//   %input | BLOCK(6) VALUES(0.1,0.01)|
//
// All these inputs have the same meaning:
//   %input | BLOCK(2) ; or $OMEGA VARIANCE COVARIANCE BLOCK(2)\n0.64\n-0.24 0.58|
//   %input | STANDARD BLOCK(2)\n0.8\n-0.24 0.762|
//   %input | STANDARD CORRELATION BLOCK(2)\n0.8\n-0.394 0.762|
//   %input | VARIANCE CORRELATION BLOCK(2)\n0.64\n-0.394 0.58|
//   %input | CHOLESKY BLOCK(2)\n0.8\n-0.3 0.7|

root : _empty_lines _record _trailing_comments? WS? NEWLINE*

_record : WS? diagonal? (_sep diag_item)+                               // form 1
     | _options? block _sep _options_right? _items                      // form 2
     | _options? block _sep _options_right? same                        // form 3
     | _options? bare_block _sep _options_right? same                   // form 3 w/o n
     | _options? block _sep _options_right? values _sep _options_right? // form 4

_trailing_comments: (NEWLINE+ | WS)? _trailing_comments_loop
_trailing_comments_loop: _trailing_comment (NEWLINE+ _trailing_comments_loop)?
_trailing_comment: comment

_options: (_sep _opt)+ _sep
_options_right: (_opt _sep)+

diag_item: init (_sep _diag_option)*
         | _lpar (_diag_option _sep)* init (_sep _diag_option)* _rpar n?
_diag_option: FIX | SD | VAR

// repeat of init or comment (form 1 & 2)
_items : omega _more_items?
_more_items : _sep_comment _items

// can be tied to WHOLE record or ONE INIT
_opt     : FIX | VAR | SD | CORR | COV | CHOLESKY
FIX      : "FIXED" | "FIXE" | "FIX"
VAR      : "VARIANCE" | "VARIANC" | "VARIAN" | "VARIA" | "VARI" | "VAR" | "VA" | "V"
SD       : "STANDARD" | "STANDAR" | "STANDA" | "STAND" | "STAN" | "STA" | "ST" | "SD" | "S"
CORR     : "CORRELATION" | "CORRELATIO" | "CORRELATI" | "CORRELAT" | "CORRELA" | "CORREL" | "CORRE" | "CORR" | "COR"
COV      : "COVARIANCE" | "COVARIANC" | "COVARIAN" | "COVARIA" | "COVARI" | "COVAR" | "COVA" | "COV"
CHOLESKY : "CHOLESKY" | "CHOLESK" | "CHOLES" | "CHOLE" | "CHOL" | "CHO"

// parentheses sizes (optional for SAME and BLOCK with SAME)
block       : ("BLOCK" | "BLOC" | "BLO") _empty_lines _lpar size _rpar
bare_block  : ("BLOCK" | "BLOC" | "BLO")
same        : "SAME" (_lpar size _rpar)?
diagonal    : ("DIAGONAL" | "DIAGONA" | "DIAGON" | "DIAGO" | "DIAG" | "DIA") _empty_lines _lpar size _rpar
values      : "VALUES" _lpar diag sep odiag _rpar

size  : INT
diag  : NUMERIC
odiag : NUMERIC

// separator and clustering of init values
sep : WS | WS? "," WS?
_lpar : "(" WS?
_rpar : WS? ")"

// omega init value
omega : init (_sep _opt)*
      | _lpar init (_sep _opt)* _sep _rpar n?
      | _lpar (_opt _sep)* init _rpar n?

init  : NUMERIC
n     : "x" INT

// common misc rules
_empty_lines: _line_ending* WS?
_line_ending: WS? COMMENT? NEWLINE
_sep: (WS | _line_ending+ WS?)
_line_ending_comment: WS? comment? NEWLINE
_sep_comment: (WS | _line_ending_comment+ WS?)
comment: COMMENT

%import .definitions (COMMENT, INT, NEWLINE, NUMERIC, WS)

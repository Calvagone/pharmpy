// $THETA  value1  [value2]  [value3] ...
//          [(value)xn]
//          [NUMBERPOINTS=n]
//          [ABORT|NOABORT|NOABORTFIRST]
//
// 5 legal forms
//   1. init [FIXED]
//   2. ([low,] init [,up] [FIXED])
//   3. ([low,] init [,up]) [FIXED]  // MUST have "low" if "up" exists
//   4. (low,,up)  // has NO init
//   5. (value)xn  // value is inside parenthesis of form 2-4
//
// Rules
//   1. "low" and "up" can be skipped with -INF/INF (default).
//   2. "FIXED" requires "low" & "up" to equal "init", insofar they appear.
//   3. "FIXED" implied if "low"="init"="up".
//   4. There can be multiple "FIXED" for one THETA
//   5. "FIXED" can appear almost anywhere and multiple times in a row
//      when parentheses are used

root : _empty_lines _options? _end?

_options: (_options _sep)? (theta | option)

_empty_lines: _sep?

_sep: ws
    | ws newline
    | ws COMMENT newline
    | newline
    | COMMENT newline

_end: COMMENT
    | ws COMMENT?
    | ws newline COMMENT?
    | ws COMMENT newline COMMENT?
    | newline COMMENT?
    | COMMENT newline COMMENT?

newline: (newline COMMENT?)? NEWLINE WS?
ws: WS

?option : KEY WS? "=" WS? VALUE -> option
        | VALUE                   -> option

theta : init (WS FIX)?                    // form 1
      | _lpar _inside _rpar _after?       // form 2+3+4+5

_inside: _init            // form 2+3+5 (init)
       | _low (WS? sep WS? _lfixes?)? init _rfixes? (WS? sep)?    // form 2+3+5 (low, init)
       | _low (WS? sep WS? _lfixes?)? init _rfixes? (WS? sep WS?  _lfixes?)? up _rfixes?  // form 2+3+5 (low, init, up?)
       | _low WS? sep WS? sep WS? _lfixes? up _rfixes?        // form 4+5 (low, up)

_init: _lfixes? init _rfixes?
_low: _lfixes? low _rfixes?

sep : ","
_lpar : "(" WS?
_rpar : WS? ")"

_after: n | WS? FIX

_fixes: (_fixes WS)? FIX
_lfixes: _fixes WS
_rfixes: WS _fixes
_lrfixes: WS _fixes WS


init : NUMERIC
low  : NUMERIC | NEG_INF
up   : NUMERIC | POS_INF
n    : "x" INT

FIX : "FIXED" | "FIXE" | "FIX"

// generic option terminals (key/value)
KEY   : /(?!([0-9]|\(|FIX))\w+/
VALUE : /(?!([0-9]|\(|FIX))[^\s=;]+/


%import .definitions (INT, COMMENT, NEWLINE, NUMERIC, WS)

NEG_INF: "-INF"i | "-1000000"i
POS_INF: "INF"i | "1000000"i

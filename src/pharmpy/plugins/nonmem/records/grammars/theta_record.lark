// $THETA  value1  [value2]  [value3] ...
//          [(value)xn]
//          [NUMBERPOINTS=n]
//          [ABORT|NOABORT|NOABORTFIRST]
//
// 5 legal forms
//   1. init [FIXED]
//   2. ([low,] init [,up] [FIXED])
//   3. ([low,] init [,up]) [FIXED]  // MUST have "low" if "up" exists
//   4. (low,,up)  // has NO init
//   5. (value)xn  // value is inside parenthesis of form 2-4
//
// Rules
//   1. "low" and "up" can be skipped with -INF/INF (default).
//   2. "FIXED" requires "low" & "up" to equal "init", insofar they appear.
//   3. "FIXED" implied if "low"="init"="up".
//   4. There can be multiple "FIXED" for one THETA
//   5. "FIXED" can appear almost anywhere and multiple times in a row
//      when parentheses are used

root : _begin (_options _end)?

_options : (_options _sep)? (theta | option)

_begin : _sep?
_end : WS? (COMMENT? _newlines)? COMMENT?
_sep : WS | WS? COMMENT? _newlines

_newlines : (_newlines COMMENT?)? NEWLINE WS?

?option : KEY WS? "=" WS? VALUE -> option
        | VALUE                 -> option

theta : init (WS FIX)?                    // form 1
      | _lpar _inside _rpar _after?       // form 2+3+4+5

_inside: _init_or_low _rest

_rest: // (empty, NO rest) form 2+3+5 (init)
     | (WS | WS? "," WS? _lfixes?) init _rfixes? (WS? ",")?    // form 2+3+5 (low, init)
     | (WS | WS? "," WS? _lfixes?) init _rfixes? (WS? "," WS?  _lfixes?)? up _rfixes?  // form 2+3+5 (low, init, up?)
     | WS? "," WS? "," WS? _lfixes? up _rfixes?        // form 4+5 (low, up)

_init_or_low : _lfixes? init_or_low _rfixes?

_lpar : "(" WS?
_rpar : WS? ")"

_after: n | WS? FIX

_fixes: (_fixes WS)? FIX
_lfixes: _fixes WS
_rfixes: WS _fixes


init_or_low: NUMERIC | NEG_INF // Hack to get around LALR(1) limitations
init : NUMERIC
low  : NUMERIC | NEG_INF
up   : NUMERIC | POS_INF
n    : "x" INT

NEG_INF: "-INF"i | "-1000000"
POS_INF: "INF"i | "1000000"

FIX : "FIXED" | "FIXE" | "FIX"

// generic option terminals (key/value)
KEY   : /(?!([0-9]|\(|FIX))\w+/
VALUE : /(?!([0-9]|\(|FIX))[^\s=;]+/

%import .definitions (INT, COMMENT, NEWLINE, NUMERIC, WS)
